/*
 * Copyright 2019, TeamDev. All rights reserved.
 *
 * Redistribution and use in source and/or binary forms, with or without
 * modification, must retain the above copyright notice and the following
 * disclaimer.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

plugins {
    id 'io.spine.tools.gradle.bootstrap' version '0.15.6' apply false
    id 'net.ltgt.errorprone' version '0.8.1' apply false
}

//noinspection GroovyAssignabilityCheck
ext {
    spineVersion = '1.0.0-SNAPSHOT'
    junit5Version = '5.5.1'
}

//noinspection GroovyAssignabilityCheck
subprojects {
    apply plugin: 'io.spine.tools.gradle.bootstrap'

    spine.enableJava().server()

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    apply plugin: 'net.ltgt.errorprone'
    apply from: "$rootDir/gradle/static-analysis.gradle"
    apply from: "$rootDir/gradle/idea.gradle"

    forceConfiguration(project)
    dependencies {
        errorprone "com.google.errorprone:error_prone_core:2.3.3"
        errorproneJavac "com.google.errorprone:javac:9+181-r4173-1"
        implementation (
                "com.google.guava:guava:28.0-jre",
                "org.checkerframework:checker-qual:2.9.0"
        )
        runtimeOnly "io.grpc:grpc-netty:1.22.0"
        testImplementation (
                "org.junit.jupiter:junit-jupiter-api:$junit5Version",
                "org.junit.jupiter:junit-jupiter-params:$junit5Version",
                "org.apiguardian:apiguardian-api:1.1.0",

                "io.spine:spine-testutil-server:$spineVersion"
        )
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
    }

    tasks.withType(JavaCompile) {

        // Explicitly states the encoding of the source and test source files, ensuring
        // correct execution of the `javac` task.
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

        // Configure Error Prone:
        // 1. Exclude generated sources from being analyzed by Error Prone.
        // 2. Turn the check off until Error Prone can handle `@Nested` JUnit classes.
        //    See issue: https://github.com/google/error-prone/issues/956
        // 3. Turn off checks which report unused methods and unused method parameters.
        //    See issue: https://github.com/SpineEventEngine/config/issues/61
        //
        // For more config details see:
        //    https://github.com/tbroyer/gradle-errorprone-plugin/tree/master#usage

        options.errorprone.errorproneArgs.addAll(
                '-XepExcludedPaths:.*/generated/.*',
                '-Xep:ClassCanBeStatic:OFF',
                '-Xep:UnusedMethod:OFF',
                '-Xep:UnusedVariable:OFF',
                '-Xep:CheckReturnValue:OFF')
    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
    }
}
